// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application'
}

description = 'demos, examples, and non-automated tests'

mainClassName = 'jme3utilities.test.wes.FlashMobDemo'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    implementation 'com.github.stephengold:Acorus:0.9.7'
    implementation 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-plugins:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jme3Version

    // WesExamples doesn't use jme3-jogg
    //  -- it is included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version

    //implementation 'com.github.stephengold:Wes' + wesVersion
    implementation project(':WesLibrary')
}

// cleanup tasks

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')

task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}

// tasks to run the example apps

task runFlashMobDemo(type: JavaExec) {
    mainClass = 'jme3utilities.test.wes.FlashMobDemo'
}
task runReverseAnimation(type: JavaExec) {
    mainClass = 'jme3utilities.test.wes.ReverseAnimation'
}
task runTrimAnimation(type: JavaExec) {
    mainClass = 'jme3utilities.test.wes.TrimAnimation'
}
