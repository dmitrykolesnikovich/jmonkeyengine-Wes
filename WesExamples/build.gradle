// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application'
}

mainClassName = 'jme3utilities.test.wes.FlashMobDemo'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    implementation heartCoordinates
    implementation 'com.github.stephengold:Acorus:0.9.9'
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-plugins:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jme3Version

    // WesExamples doesn't use jme3-jogg
    //  -- it is included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version

    // LWJGL 3.3 dependencies that were omitted from JME v3.5:
    runtimeOnly 'org.lwjgl:lwjgl:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-glfw:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-jemalloc:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-openal:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-opengl:' + lwjgl3Version + ':natives-macos-arm64'

    //implementation 'com.github.stephengold:Wes' + wesVersion
    implementation project(':WesLibrary')
}

// cleanup tasks

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')

task cleanDLLs(type: Delete) { // Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) { // macOSX native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) { // Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) { // Acorus output directory
    delete 'Written Assets'
}

// tasks to run the example apps

task ConvertToInPlace(type: JavaExec) {
    mainClass = 'jme3utilities.test.wes.ConvertToInPlace'
}
task FlashMobDemo(type: JavaExec) {
    mainClass = 'jme3utilities.test.wes.FlashMobDemo'
}
task ReverseAnimation(type: JavaExec) {
    mainClass = 'jme3utilities.test.wes.ReverseAnimation'
}
task TrimAnimation(type: JavaExec) {
    mainClass = 'jme3utilities.test.wes.TrimAnimation'
}
